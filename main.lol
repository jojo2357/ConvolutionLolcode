HAI 1.3
	HOW IZ I MAKEMATRIX1 YR N
		I HAS A MATRIX ITZ A BUKKIT
		IM IN YR LOOP UPPIN YR INDEX TIL BOTH SAEM INDEX N
			MATRIX HAS A SRS INDEX ITZ 1 BTW PRODUKT OF INDEX AN INDEX
		IM OUTTA YR LOOP
		FOUND YR MATRIX
	IF U SAY SO

	HOW IZ I MAKEMATRIX2 YR N
		I HAS A MATRIX ITZ A BUKKIT
		IM IN YR LOOP UPPIN YR INDEX TIL BOTH SAEM INDEX N
			MATRIX HAS A SRS INDEX ITZ 1 BTW DIFF OF DIFF OF N AN INDEX AN 1
		IM OUTTA YR LOOP
		FOUND YR MATRIX
	IF U SAY SO

	HOW IZ I CONVOLUDE YR MAT_1 AN YR MAT_2 AN YR N_1 AN YR N_2
		I HAS A MATRIX ITZ A BUKKIT
		I HAS A NEW_LEN ITZ DIFF OF SUM OF N_1 AN N_2 AN 1
		IM IN YR LOOP UPPIN YR INDEX TIL BOTH SAEM INDEX NEW_LEN
			I HAS A START_2 ITZ BIGGR OF 0 AN SUM OF DIFF OF INDEX AN N_1 AN 1
			I HAS A END_2 ITZ SMALLR OF SUM OF INDEX AN 1 AN N_2
			I HAS A START_1 ITZ SMALLR OF INDEX AN DIFF OF N_1 AN 1
			MATRIX HAS A SRS INDEX ITZ 0
			IM IN YR LOOP UPPIN YR SMALLDEX TIL BOTH SAEM SMALLDEX DIFF OF END_2 AN START_2
				MATRIX'Z SRS INDEX R SUM OF MATRIX'Z SRS INDEX AN PRODUKT OF MAT_1'Z SRS START_1 AN MAT_2'Z SRS SUM OF SMALLDEX AN START_2
				START_1 R DIFF OF START_1 AN 1
			IM OUTTA YR LOOP
		IM OUTTA YR LOOP
		FOUND YR MATRIX
	IF U SAY SO

	I HAS A N ITZ 5
	I HAS A MATRIX_1 ITZ A BUKKIT
	I HAS A MATRIX_2 ITZ A BUKKIT
	I HAS A CONVOLUTION_RESULT ITZ A BUKKIT

	MATRIX_1 R I IZ MAKEMATRIX1 YR N MKAY
	MATRIX_2 R I IZ MAKEMATRIX2 YR N MKAY

	CONVOLUTION_RESULT R I IZ CONVOLUDE YR MATRIX_1 AN YR MATRIX_2 AN YR N AN YR N MKAY

	IM IN YR LOOP UPPIN YR INDEX TIL BOTH SAEM INDEX DIFF OF PRODUKT OF 2 AN N AN 1
		VISIBLE CONVOLUTION_RESULT'Z SRS INDEX
	IM OUTTA YR LOOP
KTHXBYE